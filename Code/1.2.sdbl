//Задача 1.2
ВЫБРАТЬ
	РасходнаяНакладнаяСписокНоменклатуры.Номенклатура КАК Номенклатура,
	СУММА(РасходнаяНакладнаяСписокНоменклатуры.Количество) КАК Требуется,
	СУММА(РасходнаяНакладнаяСписокНоменклатуры.Сумма) КАК Продажа
ПОМЕСТИТЬ ВТТребование
ИЗ
	Документ.РасходнаяНакладная.СписокНоменклатуры КАК РасходнаяНакладнаяСписокНоменклатуры
ГДЕ
	РасходнаяНакладнаяСписокНоменклатуры.Ссылка = &Документ

СГРУППИРОВАТЬ ПО
	РасходнаяНакладнаяСписокНоменклатуры.Номенклатура

ИНДЕКСИРОВАТЬ ПО
	Номенклатура
;

////////////////////////////////////////////////////////////////////////////////
ВЫБРАТЬ
	Требование.Номенклатура КАК Номенклатура,
	Требование.Требуется КАК Требуется,
	Требование.Продажа КАК Продажа,
	ОстаткиНоменклатурыОстатки.Партия,
	ЕСТЬNULL(ОстаткиНоменклатурыОстатки.КоличествоОстаток, 0) КАК Остаток,
	ЕСТЬNULL(ОстаткиНоменклатурыОстатки.СуммаОстаток, 0) КАК СуммаОстатка,
	ВЫБОР
		КОГДА Требование.Номенклатура.ВидНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ВидыНоменклатуры.Товар)
			ТОГДА ИСТИНА
		ИНАЧЕ ЛОЖЬ
	КОНЕЦ КАК ЭтоТовар,
	ВЫБОР
		КОГДА ОстаткиНоменклатурыОстатки.Партия = &Партия
			ТОГДА 1
		ИНАЧЕ 2
	КОНЕЦ КАК Порядок
ИЗ
	ВТТребование КАК Требование
		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ОстаткиНоменклатуры.Остатки(
				&Момент,
				Номенклатура В
					(ВЫБРАТЬ РАЗЛИЧНЫЕ
						ВТТребование.Номенклатура
					ИЗ
						ВТТребование КАК ВТТребование)) КАК ОстаткиНоменклатурыОстатки
		ПО Требование.Номенклатура = ОстаткиНоменклатурыОстатки.Номенклатура

УПОРЯДОЧИТЬ ПО
	Порядок,
	ОстаткиНоменклатурыОстатки.Партия.МоментВремени  ВОЗР 
ИТОГИ
	МАКСИМУМ(Требуется),
	МАКСИМУМ(Продажа),
	СУММА(Остаток)
ПО
	Номенклатура