ВЫБРАТЬ 
   РасходнаяНакладнаяСписокНоменклатуры.Номенклатура КАК Номенклатура, 
   СУММА(РасходнаяНакладнаяСписокНоменклатуры.Количество) КАК Количество, 
   СУММА(РасходнаяНакладнаяСписокНоменклатуры.Сумма) КАК Сумма, 
   РасходнаяНакладнаяСписокНоменклатуры.Партия КАК Партия, 
   МИНИМУМ(РасходнаяНакладнаяСписокНоменклатуры.НомерСтроки) КАК НомерСтроки 
 ПОМЕСТИТЬ тТоварыИПартии 
 ИЗ 
   Документ.РасходнаяНакладная.СписокНоменклатуры КАК РасходнаяНакладнаяСписокНоменклатуры 
 ГДЕ 
   РасходнаяНакладнаяСписокНоменклатуры.Ссылка = &Ссылка 

 СГРУППИРОВАТЬ ПО 
   РасходнаяНакладнаяСписокНоменклатуры.Номенклатура, 
   РасходнаяНакладнаяСписокНоменклатуры.Партия 

 ИНДЕКСИРОВАТЬ ПО 
   Номенклатура, 
   Партия 
 ; 

 //////////////////////////////////////////////////////////////////////////////// 
 ВЫБРАТЬ 
   тТоварыИПартии.Номенклатура КАК Номенклатура, 
   СУММА(тТоварыИПартии.Количество) КАК Количество, 
   СУММА(тТоварыИПартии.Сумма) КАК Сумма, 
   МИНИМУМ(тТоварыИПартии.НомерСтроки) КАК НомерСтроки 
 ПОМЕСТИТЬ тТовары 
 ИЗ 
   тТоварыИПартии КАК тТоварыИПартии 

 СГРУППИРОВАТЬ ПО 
   тТоварыИПартии.Номенклатура 

 ИНДЕКСИРОВАТЬ ПО 
   Номенклатура 
 ; 

 //////////////////////////////////////////////////////////////////////////////// 
 ВЫБРАТЬ 
   тТовары.Номенклатура КАК Номенклатура, 
   МАКСИМУМ(тТовары.Количество) КАК КолвоДок, 
   МАКСИМУМ(тТовары.Сумма) КАК СуммаДок, 
   ОстаткиНоменклатурыОстатки.Партия КАК Партия, 
   СУММА(ОстаткиНоменклатурыОстатки.КоличествоОстаток) КАК КолвоОстаток, 
   СУММА(ОстаткиНоменклатурыОстатки.СебестоимостьОстаток) КАК СебестОстаток, 
   ВЫБОР 
    КОГДА ОстаткиНоменклатурыОстатки.Партия = тТоварыИПартии.Партия 
     ТОГДА 0 
    ИНАЧЕ 1 
   КОНЕЦ КАК Приоритет, 
   тТовары.НомерСтроки КАК НомерСтроки 
 ИЗ 
   тТовары КАК тТовары 
    ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ОстаткиНоменклатуры.Остатки( 
      &момент, 
      Номенклатура В 
       (ВЫБРАТЬ 
        т.Номенклатура 
       ИЗ 
        тТовары КАК т)) КАК ОстаткиНоменклатурыОстатки 
     ЛЕВОЕ СОЕДИНЕНИЕ тТоварыИПартии КАК тТоварыИПартии 
     ПО ОстаткиНоменклатурыОстатки.Номенклатура = тТоварыИПартии.Номенклатура 
      И ОстаткиНоменклатурыОстатки.Партия = тТоварыИПартии.Партия 
    ПО тТовары.Номенклатура = ОстаткиНоменклатурыОстатки.Номенклатура 

 СГРУППИРОВАТЬ ПО 
   ОстаткиНоменклатурыОстатки.Партия, 
   тТовары.Номенклатура, 
   тТовары.НомерСтроки, 
   ВЫБОР 
    КОГДА ОстаткиНоменклатурыОстатки.Партия = тТоварыИПартии.Партия 
     ТОГДА 0 
    ИНАЧЕ 1 
   КОНЕЦ 

 УПОРЯДОЧИТЬ ПО 
   Номенклатура, 
   Приоритет, 
   Партия УБЫВ 
 ИТОГИ 
   МАКСИМУМ(КолвоДок), 
   МАКСИМУМ(СуммаДок), 
   СУММА(КолвоОстаток), 
   СУММА(СебестОстаток), 
   МИНИМУМ(НомерСтроки) 
 ПО 
   Номенклатура 
 АВТОУПОРЯДОЧИВАНИЕ 
 